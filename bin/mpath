#!/usr/bin/env perl

use 5.010001;
use strict;
use warnings;

use Getopt::Long qw(GetOptions);
use Module::Path qw(module_path);
use Pod::Usage qw(pod2usage);

# VERSION

my %opts;
GetOptions(\%opts,
           'help|h|?',
           'quiet|q',
           'full|f!',
           'abs',
           'all',
           'find-pm!'  => \$opts{find_pm},
           'find-pmc!' => \$opts{find_pmc},
           'find-pod!' => \$opts{find_pod},
           'pod'       => sub {
               $opts{find_pod} = 1;
               $opts{find_pm}  = 0;
               $opts{find_pmc} = 0;
           },
       )
    or pod2usage 2;

if ($opts{help}) {
    pod2usage(
        -verbose => 2,
        -exitval => 0,
    )
}
elsif (!@ARGV) {
    warn "mpath: Please specify at least one module\n";
    exit 99;
}

$opts{full} //= @ARGV > 1 ? 1:0;

my $all_found = 1;

my %mpopts = (
    abs      => $opts{abs},
    all      => $opts{all},
    find_pm  => $opts{find_pm},
    find_pmc => $opts{find_pmc},
    find_pod => $opts{find_pod},
);

for my $module (@ARGV) {
    my @paths;
    if ($opts{all}) {
        @paths = module_path($module, \%mpopts);
    } else {
        my $path = module_path($module, \%mpopts);
        push @paths, $path if defined $path;
    }

    if (!@paths) {
        $all_found = 0;
        warn "mpath: $module not found\n" unless $opts{quiet};
    } else {
        for (@paths) {
            print "$module\t" if $opts{full};
            print "$_\n";
        }
    }
}

my $status = $all_found ? 0 : 1;
exit $status;

# ABSTRACT: Display the path to a perl module (installed locally)
# PODNAME: mpath

=head1 USAGE

  mpath [OPTIONS] MODULE [MODULE ...]


=head1 EXAMPLE

  % mpath Module::Path
  /usr/local/lib/perl5/site_perl/5.16.0/Module/Path.pm

  % mpath --full Module::Path
  Module::Path /usr/local/lib/perl5/site_perl/5.16.0/Module/Path.pm

  % mpath Moose Moo
  /usr/local/lib/perl5/site_perl/5.16.0/darwin-2level/Moose.pm
  /usr/local/lib/perl5/site_perl/5.16.0/Moo.pm


=head1 DESCRIPTION

mpath displays the full path to a perl module on the local system. It uses the
C<module_path()> function from L<Module::Path> to get the path.

If one of the module wasn't found, mpath will exit with the exit code 1 and
print the following message (you can silence it with the option B<--quiet>):

  % mpath Foo::Bar
  mpath: Foo::Bar not found

or:

  % mpath Moose Foo::Bar
  Moose    /usr/local/lib/perl5/site_perl/5.16.0/darwin-2level/Moose.pm
  mpath: Foo::Bar not found

=head1 OPTIONS

=over

=item B<-h>, B<-?>, B<--help>

Print this help message and exit.

=item B<-f>, B<--full>

Print module name before the path. For convenience this is turned on by default
if there are more than one argument (but you can still turn it off using
C<--nofull>).

=item B<-q>, B<--quiet>

Don't print any error when one of the module requested could not be found.

=item B<--abs>

Absolutize paths.

=item B<--all>

Find all found paths in C<@INC> instead of the first one.

=item B<--[no]find-pm>

Find C<.pm> files. Default is on.

=item B<--[no]find-pmc>

Find C<.pmc> files. Default is on.

=item B<--[no]find-pod>

Find C<.pod> files.

=item B<--pod>

Equivalent to C<--find-pod --nofind-pm --nofind-pmc>.

=back


=head1 SEE ALSO

L<Alt::Module::Path::SHARYANTO>

L<Module::Path>
